# Start every shell with a random my little horse ebooks
if [[ $TERM_PROGRAM != "vscode" ]]; then
  FORTUNES=$BREW_PREFIX/share/games/fortunes
  echo; fortune $FORTUNES/horse_ebooks $FORTUNES/weird_twitter | ponysay -b unicode 2>/dev/null | pr -o 3 -t; echo
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
() { [[ -r $1 ]] && source $1 } ${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-$USERNAME.zsh

# Source zshenv again after macOS /etc/zprofile nukes PATH
source ~/.zshenv

# Load znap so everything else can be loaded in the background
source ~/.zsh/znap/zsh-snap/znap.zsh

# znap source marlonrichert/zsh-autocomplete
znap source zsh-users/zsh-autosuggestions
znap source zsh-users/zsh-syntax-highlighting
# znap source softmoth/zsh-vim-mode
znap source zsh-users/zsh-history-substring-search
znap source scriptingosx/mac-zsh-completions
znap source MichaelAquilina/zsh-you-should-use

# bind the history substring search to up and down arrows
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# only match command starts on up and down
HISTORY_SUBSTRING_SEARCH_PREFIXED=true
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND=(bold)
if [[ -n $GHOSTTY_RESOURCES_DIR ]]; then
  znap eval ghostty 'cat "$GHOSTTY_RESOURCES_DIR"/shell-integration/zsh/ghostty-integration'
fi

# switch edit mode back to emacs despite EDITOR being vim
# this has to come before the znap evals so it can be partially overwritten
bindkey -e

# for some reason tmux loses forward delete bind
bindkey "^[[3~" delete-char

znap eval zoxide 'zoxide init --cmd j zsh'
znap eval nodenv 'nodenv init -'
znap eval fzf    'fzf --zsh'
znap eval atuin  'atuin init zsh --disable-up-arrow'

znap fpath _kubectl 'kubectl completion zsh'
znap fpath _rustup  'rustup completions zsh'
znap fpath _cargo   'rustup completions zsh cargo'
znap fpath _atuin   'atuin gen-completions --shell zsh'
znap fpath _posh    'posh completion zsh'

comppaths=(
  ~/.zsh/functions
  $BREW_PREFIX/share/zsh-completions
  $BREW_PREFIX/share/zsh/site-functions
)

for comppath in $comppaths; do
  fpath+=$comppath
  autoload -U $comppath/*(:t)
done

for file in ~/.zsh/*.zsh; do
  znap eval $file(:t) "cat $file"
done

# Globbing a bit more like bash
setopt NO_CASE_GLOB
setopt GLOB_COMPLETE

# Make delete-by-word stop on punctuation like other editors
WORDCHARS=""

# Naming a directory runs cd for you
setopt AUTO_CD

# Shared history across all shells, updated after every command
HISTFILE=${ZDOTDIR:-$HOME}/.zsh_history
export HISTSIZE=1000000000
export SAVEHIST=$HISTSIZE
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE

# Break words on forward slash, so delete-word stops on directories
WORDCHARS=${WORDCHARS/\/}

# Show substitutions before running them
setopt HIST_VERIFY

# partial completion suggestions
zstyle ':completion:*' list-suffixes
zstyle ':completion:*' expand prefix suffix

# case insensitive path-completion
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*'

export EDITOR='mvim -f --nomru -c "au VimLeave * !open -b $__CFBundleIdentifier"'
export PAGER="less" # page with less not more
export BROWSER="open" # opens URLs in the default OS X browser
export LESS="-XRF" # Fix less to not clear screen on exit, show color, exit if < 1 screen
export CLICOLOR=true # ls with color
export BAT_THEME="Solarized (dark)"
export RI="-f ansi" # ri with color
export FZF_DEFAULT_COMMAND="rg --files" # only choose from files rg would search

# Tell Kaleidoscope where to find itself
export KALEIDOSCOPE_DIR=$HOME/src/indirect/Kaleidoscope

# RubyGems.org staging and production access via kubectl
export KUBECONFIG=$KUBECONFIG:~/.kube/rubygems.config

# Set up a default ruby version
export DEFAULT_RUBY_VERSION=$(ls ~/.rubies | tail -n1)
chruby $DEFAULT_RUBY_VERSION

# Use sekey's agent to store SSH keys in the secure enclave
export SSH_AUTH_SOCK=$HOME/.sekey/ssh-agent.ssh

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
znap source romkatv/powerlevel10k
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
