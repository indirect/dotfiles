# Source zshenv again after macOS /etc/zprofile nukes PATH
source ~/.zshenv

# Stop here if this is e.g. cron or puma-dev
[[ ! -v TERM ]] && return 0

# Start every shell with a random my little horse ebooks
if [[ $TERM_PROGRAM != "vscode" ]]; then
  echo; $BREW_PREFIX/bin/fortune $BREW_PREFIX/share/games/fortunes/weird | ponysay -b unicode 2>/dev/null | pr -o 3 -t; echo
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
() { [[ -r $1 ]] && source $1 } ${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-$USERNAME.zsh

# Load znap so everything else can be loaded in the background
source ~/.zsh/znap/zsh-snap/znap.zsh

# znap source marlonrichert/zsh-autocomplete
znap source zsh-users/zsh-autosuggestions
znap source zsh-users/zsh-syntax-highlighting
# znap source softmoth/zsh-vim-mode
znap source zsh-users/zsh-history-substring-search
znap source scriptingosx/mac-zsh-completions
znap source MichaelAquilina/zsh-you-should-use
znap source mafredri/zsh-async

if [[ -n $GHOSTTY_RESOURCES_DIR ]]; then
  znap eval ghostty 'cat "$GHOSTTY_RESOURCES_DIR"/shell-integration/zsh/ghostty-integration'
fi

# switch edit mode back to emacs despite EDITOR being vim
# this has to come before the znap evals so it can be partially overwritten
bindkey -e

# for some reason tmux loses forward delete bind
bindkey "^[[3~" delete-char

znap eval zoxide 'zoxide init zsh'
znap eval nodenv 'nodenv init -'
znap eval fzf    'fzf --zsh'
znap eval atuin  'atuin init zsh --disable-up-arrow'
znap eval ghcs   'gh copilot alias zsh'
znap eval jj     'COMPLETE=zsh jj'
znap eval rv     'rv shell init zsh'

# bind the history substring search to up and down arrows
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# only match command starts on up and down
HISTORY_SUBSTRING_SEARCH_PREFIXED=true
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND=(bold)

znap fpath _kubectl 'kubectl completion zsh'
znap fpath _rustup  'rustup completions zsh'
znap fpath _cargo   'rustup completions zsh cargo'
znap fpath _atuin   'atuin gen-completions --shell zsh'
znap fpath _posh    'posh completion zsh'
znap fpath _rv      'rv shell completions zsh'

comppaths=(
  ~/.zsh/functions
  $BREW_PREFIX/share/zsh-completions
  $BREW_PREFIX/share/zsh/site-functions
)

for comppath in $comppaths; do
  fpath+=$comppath
  autoload -U $comppath/*(:t)
done

for file in ~/.zsh/*.zsh; do
  znap eval $file(:t) "cat $file"
done

# ctrl-] to jump to a repo
zle -N fzf-src
bindkey '^]' fzf-src

# Make delete-by-word stop on punctuation like other editors
WORDCHARS=""
# Shared history across all shells, updated after every command
HISTFILE=${ZDOTDIR:-$HOME}/.zsh_history

export HISTSIZE=1000000000
export SAVEHIST=$HISTSIZE
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt AUTO_CD
setopt NO_CASE_GLOB
setopt GLOB_COMPLETE
setopt HIST_VERIFY

# partial completion suggestions
zstyle ':completion:*' list-suffixes
zstyle ':completion:*' expand prefix suffix

# case insensitive path-completion
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*'

export EDITOR="$HOME/.bin/editor"
export VISUAL="$HOME/.bin/visual"
export PAGER="less" # page with less not more
export BROWSER="open" # opens URLs in the default OS X browser
export LESS="-XRF" # Fix less to not clear screen on exit, show color, exit if < 1 screen
export CLICOLOR=true # ls with color
export BAT_THEME="Solarized (dark)"
export RI="-f ansi" # ri with color
export FZF_DEFAULT_COMMAND="rg --files --sortr created"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export KALEIDOSCOPE_DIR=$HOME/src/indirect/Kaleidoscope
export KUBECONFIG=$KUBECONFIG:~/.kube/rubygems.config
export HOMEBREW_NO_ENV_HINTS=1

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
znap source romkatv/powerlevel10k
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
