# Start every shell with a random my little horse ebooks
echo; fortune "$BREW_PREFIX/share/games/horse_fortunes" | ponysay -b unicode | pr -o 3 -t; echo

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
() { [[ -r $1 ]] && source $1 } ${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-$USERNAME.zsh

# Source zshenv again after macOS /etc/zprofile nukes PATH
source ~/.zshenv

source ~/.zsh/znap/zsh-snap/znap.zsh

# znap source marlonrichert/zsh-autocomplete
znap source zsh-users/zsh-autosuggestions
znap source zsh-users/zsh-syntax-highlighting
znap source softmoth/zsh-vim-mode
znap source zsh-users/zsh-history-substring-search
znap source scriptingosx/mac-zsh-completions
znap source MichaelAquilina/zsh-you-should-use
znap source unixorn/fzf-zsh-plugin

znap eval zoxide "zoxide init zsh"
znap eval nodenv "nodenv init -"
znap eval fzf    "cat $BREW_PREFIX/opt/fzf/shell/*.zsh"

fpath=(~/.zsh/functions $fpath)
autoload -U ~/.zsh/functions/*(.:t)

for file in ~/.zsh/*.zsh; do
  znap eval $file(.:t) "cat $file"
done

DOCKER_ETC="/Applications/Docker.app/Contents/Resources/etc"
znap fpath _docker  "cat $DOCKER_ETC/docker.zsh-completion"
znap fpath _kubectl "kubectl completion zsh"
znap fpath _rustup  "rustup  completion zsh"
znap fpath _cargo   "rustup  completion zsh cargo"

# Globbing a bit more like bash
setopt NO_CASE_GLOB
setopt GLOB_COMPLETE

# Make delete-by-word stop on punctuation like other editors
WORDCHARS=""

# Naming a directory runs cd for you
setopt AUTO_CD

# Shared history across all shells, updated after every command
HISTFILE=${ZDOTDIR:-$HOME}/.zsh_history
export HISTSIZE=1000000000
export SAVEHIST=$HISTSIZE
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE

# Break words on forward slash, so delete-word stops on directories
WORDCHARS=${WORDCHARS/\/}

# Show substitutions before running them
setopt HIST_VERIFY

export EDITOR='mvim -f --nomru -c "au VimLeave * !open -b $__CFBundleIdentifier"'
export PAGER="less" # page with less not more
export BROWSER="open" # opens URLs in the default OS X browser
export LESS="-XRF" # Fix less to not clear screen on exit, show color, exit if < 1 screen
export CLICOLOR=true # ls with color
export RI="-f ansi" # ri with color
export FZF_DEFAULT_COMMAND="rg --files" # only choose from files rg would search

# switch edit mode back to emacs despite EDITOR being vim
bindkey -e

# partial completion suggestions
zstyle ':completion:*' list-suffixes
zstyle ':completion:*' expand prefix suffix

# case insensitive path-completion
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]} l:|=* r:|=*'

# prefix search history with up and down
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search
bindkey "^[[B" down-line-or-beginning-search

# RubyGems.org staging and production access via kubectl
export KUBECONFIG=$KUBECONFIG:~/.kube/rubygems.config

# Keep go's installed files out of the way
export GOPATH=$HOME/.go

# Set up a default ruby version
export DEFAULT_RUBY_VERSION=$(ls ~/.rubies | tail -n1)
chruby $DEFAULT_RUBY_VERSION

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
znap source romkatv/powerlevel10k
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
